buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.4.RELEASE'
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.osdetector'

archivesBaseName = project.name

repositories {
    mavenLocal()
    mavenCentral()
}

// Require Java 8 to build the project.
tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
    options.warnings = false
    options.debug = true
    options.incremental = true
}

def onIdeImport = { Closure action ->
    if (System.getProperty('idea.active') == 'true' ||
        System.getProperty('eclipse.home.location') != null) {
        project.afterEvaluate {
            action.delegate = project
            action()
        }
    }
}

// Delete the source files generated by the Thrift compiler.
clean {
    [ 'main', 'test' ].each {
        delete "${project.projectDir}/src/$it/gen-java"
    }
}

// Compile the Thrift service definitions.
task compileThrift(group: 'Build',
                   description: 'Compiles the .thrift files') {
    def thriftPath = "${rootProject.projectDir}/src/build/thrift.${osdetector.classifier}"

    [ 'main', 'test' ].each { scope ->
        def inputDir = "${project.projectDir}/src/${scope}/thrift"
        def outputDir = "${project.projectDir}/src/${scope}/gen-java"

        if (project.file(inputDir).isDirectory()) {
            inputs.dir inputDir
            outputs.dir outputDir

            project.sourceSets[scope].java.srcDir outputDir

            doLast {
                project.fileTree(inputDir) {
                    include '**/*.thrift'
                }.each { sourceFile ->
                    project.mkdir(outputDir)
                    project.exec {
                        commandLine thriftPath,
                                '-gen', 'java',
                                '-out', outputDir,
                                '-I', "${sourceFile.parentFile.absolutePath}",
                                sourceFile.absolutePath
                    }
                }
            }
        }
    }
}

// Ensure the generated Thrift source files are available during the build.
project.tasks.compileJava.dependsOn(tasks.compileThrift)
onIdeImport { project.tasks.compileThrift.execute() }

springBoot {
    mainClass = 'com.linecorp.armeria.sample.SampleApplication'
}

dependencies {
    compile 'com.linecorp.armeria:armeria-spring-boot-starter-shaded:0.52.0-SNAPSHOT'
    compile 'com.linecorp.armeria:armeria-thrift-shaded:0.52.0-SNAPSHOT'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-logging'
    compile 'org.hibernate:hibernate-validator:5.4.1.Final'

    testCompile 'junit:junit:4.12'
    testCompile "org.assertj:assertj-core:3.8.0"
}
